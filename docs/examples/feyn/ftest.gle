size 16 12 

include "feyn.gle" 
! this is line number 1630 
 
! example 1 
begin translate 0 6 
    x1 = 1 
    x2 = 3 
    x3 = 5 
    x4 = 7 
    y1 = 1 
    y2 = 3 
    y3 = 5 
    xt = 4 
    yt = 2 
    yt1 = 2.5 
    yt2 = 3.5 
    ! it is usually a good idea to introduce named variables 
    ! so that you can easily tune their values 
    amove x1 y1 
    @Fermion x2 y2   ! first  fermion line 
    @Arrow 0.5 
    @Mom 0.5 
    @Fermion x1 y3   ! second fermion line 
    @Arrow 0.5 
    MomL = -MomL 
    ArrowL = -ArrowL 
    @Mom 0.5         ! opposite direction 
    MomL = -MomL 
    ArrowL = -ArrowL 
    amove x2 y2 
    @Photon x3 y2    ! photon line 
    MomD = -MomD 
    @Mom 0.5         ! to the right from the line 
    @Zigzag x4 y3    ! first  zigzag line 
    @Mom 0.5 
    MomD = -MomD 
    amove x3 y2 
    PhotonA = -PhotonA 
    @Zigzag x4 y1    ! second zigzag line 
    PhotonA = -PhotonA 
    @Mom 0.5 
    ! momenta near the arrows 
    amove x1 yt1 
    text p_{1} 
    amove x1 yt2 
    text p_{2} 
    amove x4 yt1 
    text k_{1} 
    amove x4 yt2 
    text k_{2} 
    amove xt yt 
    text p_{1}{\setfont{texcmr}+}p_{2} 
    ! there is no + in the font texmi 
end translate 
 
! example 2 
begin translate 8 6 
    x5 = 2 
    x6 = 4 
    x7 = 6 
    y0 = 1.5 
    y4 = 2.5 
    y5 = 3.5 
    amove x1 y0 
    @Double x4 y0 
    @Arrow 0.5 
    amove x5 y0+DoubleA  ! above the center 
    @Gluon x2 y2 
    @Dash2 x6 y5 x3 y2 
    @Arrow 0.5 
    @Dash2 x6 y4 x2 y2 
    @Arrow 0.5 
    amove x3 y2 
    @Gluon x7 y0+DoubleA 
end translate 
 
! example 3 
begin translate 0 0 
    x2 = 3.3 
    x3 = 4.7 
    y4 = 2.2 
    amove x1 y0 
    @Fermion x4 y0 
    @Arrow 0.26 
    @Arrow 0.5 
    @Arrow 0.74 
    amove x5 y0 
    @Gluon2 x6 y5 x7 y0 
    amove x2 y0 
    PhotonN = -1  ! to make the number of half-waves odd 
    @Photon2 x6 y4 x3 y0 
end translate
